
;civ specific structure preferences

(defrule
	(true)
=>
	(up-modify-goal phase2-barracks-cap c:= 2)
	(up-modify-goal phase3-barracks-cap c:= 2)
	(up-modify-goal phase3-range-cap c:= 2)
	(up-modify-goal phase3-stable-cap c:= 2)
	(up-modify-goal phase4-barracks-cap c:= 2)
	(up-modify-goal phase4-range-cap c:= 3)
	(up-modify-goal phase4-stable-cap c:= 3)
	(up-modify-goal phase5-barracks-cap c:= 2)
	(up-modify-goal phase5-range-cap c:= 4)
	(up-modify-goal phase5-stable-cap c:= 4)
	(up-modify-goal phase5-workshop-cap c:= 4)
	
	(disable-self)
)

;Always Train Militia/Swordsmen
(defrule
	(goal current-phase 5)
	(unit-type-count-total militiaman-line g:< desired-number-antiinfantry)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)

;Always have some Spearmen
(defrule
	(goal current-phase 5)
	(unit-type-count-total spearman-line g:< gl-one-percent)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Otherwise, train them to counter Cavalry and Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-cavalry 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total spearman-line g:< desired-number-anticavalrytrash)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Use Archers against Cavalry & Infantry
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-cavalry 1)
		(goal train-anti-infantry 1)
	)
	(unit-type-count-total bowman-line g:< desired-number-anticavalry)
	(can-train bowman-line)
=>
	(train bowman-line)
)

;Use Skirmishers against Archers & Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-archer 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total skirmisher-line g:< desired-number-antiarchertrash)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)

;Use Mangonels against Archers(but not Cavalry Archers) in addition to Skirmishers
(defrule
	(goal current-phase 5)
	(goal train-anti-archer 1)
	(unit-type-count-total mangonel-line g:< desired-number-antiarcher)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)

;Scorpions are used to counter infantry, archers, cav archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-infantry 1)
		(or
			(goal train-anti-archer 1)
			(goal train-anti-cavalryarcher 1)
		)
	)
	(unit-type-count-total scorpion-line g:< desired-number-antiarcher)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)

;Scout/Light Cavalry are used as anti-siege trash if anti-siege units cannot be afforded
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(not
		(can-afford-unit cavalry-line)
	)
	(unit-type-count-total scout-cavalry-line g:< desired-number-antisiegetrash)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)

;Cavalry are used in addition to other anti-siege measures as anti-siege units
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(unit-type-count-total cavalry-line g:< desired-number-antisiege)
	(can-train cavalry-line)
=>
	(train cavalry-line)
)

;Scout/Light Cavalry are also as anti-priest trash
(defrule
	(goal current-phase 5)
	(goal train-anti-priest 1)
	(unit-type-count-total scout-cavalry-line g:< gl-one-percent)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)

;Navy, generic train rules that affect <x>reme etc training are in military-units-common.per
;Train Quadriremes/Quinqueremes to complement our <x>remes and Hemiolia
(defrule
	(goal current-phase 5)
	(or
		(goal train-remes 1)
		(goal train-hemiolia 1)
	)
	(wood-amount > 300)
	(unit-type-count-total quadrireme-line g:< desired-number-qremes)
	(can-train quadrireme-line)
=>
	(train quadrireme-line)
)

;Train Juggernauts
(defrule
	(goal current-phase 5)
	(wood-amount > 250)
	(unit-type-count-total juggernaut-line g:< desired-number-juggernauts)
	(can-train juggernaut-line)
=>
	(train juggernaut-line)
)

(defrule
	(goal current-phase 5)
	(unit-type-count-total cretan-archer-line g:< desired-number-uniqueunittwo)
	(can-train cretan-archer-line)
=>
	(train cretan-archer-line)
)

; Upgrade Cretan Archer
(defrule
	(up-compare-goal villager-count g:>= upgrade-military-villager-requirement)
	(goal upgrade-military-lines 1)
	(can-research-with-escrow ri-elite-cretan-archer)
=>
	(release-escrow wood)
	(release-escrow gold)
	(research ri-elite-cretan-archer)
	(chat-local-to-self "research elite cretan archer")
)

;unique technologies

;Minoan Palaces, chosen if current map is not sea strategy based
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal sea-game 0)
	(can-train-with-escrow ut-minoan-palaces)
=>
	(release-escrow stone)
	(release-escrow gold)
	(train ut-minoan-palaces)
	(chat-local-to-self "research minoan palaces(UT)")
)

;Strogulla Fishing Ships, chosen if current map is sea strategy based
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal sea-game 1)
	(can-train-with-escrow ut-strogulla-fishing-ships)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-strogulla-fishing-ships)
	(chat-local-to-self "research strogulla fishing ships(UT)")
)

;Linothorax, chosen if current map is not sea strategy based
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal sea-game 0)
	(can-train-with-escrow ut-linothorax)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-linothorax)
	(chat-local-to-self "research linothorax(UT)")
)

;Cretan Pirates, chosen if current map is sea strategy based
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal sea-game 1)
	(can-train-with-escrow ut-cretan-pirates)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-cretan-pirates)
	(chat-local-to-self "research cretan pirates(UT)")
)