(defrule
	(up-timer-status farm-check-timer != timer-running)
=>
	(enable-timer farm-check-timer 30)
)

(defrule
	(and
		(goal current-phase 1)
		(and
			(up-object-type-count c: lumber-camp == 0)
			(up-object-type-count c: mill == 0)
		)
	)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 80)
	(up-modify-sn sn-wood-gatherer-percentage c:= 10)
	(up-modify-sn sn-gold-gatherer-percentage c:= 0)
	(up-modify-sn sn-stone-gatherer-percentage c:= 0)
)

(defrule
	(and
		(goal current-phase 1)
		(and
			(up-object-type-count c: lumber-camp >= 1)
			(up-object-type-count c: mill == 0)
		)
	)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 40)
	(up-modify-sn sn-wood-gatherer-percentage c:= 60)
	(up-modify-sn sn-gold-gatherer-percentage c:= 0)
	(up-modify-sn sn-stone-gatherer-percentage c:= 0)
)

(defrule
	(and
		(goal current-phase 1)
		(and
			(up-object-type-count c: lumber-camp >= 1)
			(up-object-type-count c: mill >= 1)
		)
	)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 90)
	(up-modify-sn sn-wood-gatherer-percentage c:= 10)
	(up-modify-sn sn-gold-gatherer-percentage c:= 0)
	(up-modify-sn sn-stone-gatherer-percentage c:= 0)
)

(defrule
	(goal current-phase 2)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 50)
	(up-modify-sn sn-wood-gatherer-percentage c:= 40)
	(up-modify-sn sn-gold-gatherer-percentage c:= 10)
	(up-modify-sn sn-stone-gatherer-percentage c:= 0)
)

(defrule
	(and
		(goal current-phase 3)
		(and
			(goal age-up-finished EARLY-ANTIQUITY)
			(and
				(goal age-up-started EARLY-ANTIQUITY)
				(and
					(building-type-count-total market == 0)
					(building-type-count-total blacksmith == 0)
				)
			)
		)
	)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 20)
	(up-modify-sn sn-wood-gatherer-percentage c:= 80)
	(up-modify-sn sn-gold-gatherer-percentage c:= 0)
	(up-modify-sn sn-stone-gatherer-percentage c:= 0)
)

(defrule
	(and
		(goal current-phase 3)
		(and
			(goal age-up-finished EARLY-ANTIQUITY)
			(and
				(goal age-up-started EARLY-ANTIQUITY)
				(and
					(building-type-count-total market > 0)
					(building-type-count-total blacksmith > 0)
				)
			)
		)
	)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 80)
	(up-modify-sn sn-wood-gatherer-percentage c:= 10)
	(up-modify-sn sn-gold-gatherer-percentage c:= 10)
	(up-modify-sn sn-stone-gatherer-percentage c:= 0)
)

(defrule
	(and
		(goal current-phase 3)
		(and
			(goal age-up-finished EARLY-ANTIQUITY)
			(and
				(goal age-up-started MIDDLE-ANTIQUITY)
				(and
					(building-type-count-total market > 0)
					(building-type-count-total blacksmith > 0)
				)
			)
		)
	)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 40)
	(up-modify-sn sn-wood-gatherer-percentage c:= 40)
	(up-modify-sn sn-gold-gatherer-percentage c:= 10)
	(up-modify-sn sn-stone-gatherer-percentage c:= 10)
)

(defrule
	(and
		(current-age-time < 120)
		(and
			(goal current-phase 4)
			(wood-amount < 150)
		)
	)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 10)
	(up-modify-sn sn-wood-gatherer-percentage c:= 80)
	(up-modify-sn sn-gold-gatherer-percentage c:= 5)
	(up-modify-sn sn-stone-gatherer-percentage c:= 5)
)

(defrule
	(and
		(current-age-time > 120)
		(and
			(goal current-phase 4)
			(wood-amount > 150)
		)
	)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 40)
	(up-modify-sn sn-wood-gatherer-percentage c:= 45)
	(up-modify-sn sn-gold-gatherer-percentage c:= 10)
	(up-modify-sn sn-stone-gatherer-percentage c:= 5)
)

(defrule
	(goal current-phase 5)
=>
	(up-modify-sn sn-food-gatherer-percentage c:= 40)
	(up-modify-sn sn-wood-gatherer-percentage c:= 45)
	(up-modify-sn sn-gold-gatherer-percentage c:= 10)
	(up-modify-sn sn-stone-gatherer-percentage c:= 5)
)

(defrule
	(up-compare-sn mill-max-distance != mill-max-distance)
=>
	(up-modify-sn sn-mill-max-distance g:= mill-max-distance)
)

(defrule
	(up-compare-sn sn-lumber-camp-max-distance != lumbercamp-max-distance)
=>
	(up-modify-sn sn-lumber-camp-max-distance g:= lumbercamp-max-distance)
)

(defrule
	(up-compare-sn sn-mining-camp-max-distance != miningcamp-max-distance)
=>
	(up-modify-sn sn-mining-camp-max-distance g:= miningcamp-max-distance)
)

(defrule
	(up-compare-sn sn-maximum-hunt-drop-distance != max-hunt-distance)
=>
	(up-modify-sn sn-maximum-hunt-drop-distance g:= max-hunt-distance)
)

(defrule
	(up-compare-sn sn-maximum-food-drop-distance != max-food-distance)
=>
	(up-modify-sn sn-maximum-food-drop-distance g:= max-food-distance)
)

(defrule
	(up-compare-sn sn-maximum-wood-drop-distance != max-wood-distance)
=>
	(up-modify-sn sn-maximum-wood-drop-distance g:= max-wood-distance)
)

(defrule
	(up-compare-sn sn-maximum-stone-drop-distance != max-stone-distance)
=>
	(up-modify-sn sn-maximum-stone-drop-distance g:= max-stone-distance)
)

(defrule
	(up-compare-sn sn-maximum-gold-drop-distance != max-gold-distance)
=>
	(up-modify-sn sn-maximum-gold-drop-distance g:= max-gold-distance)
)

;run once, timer handles it afterwards
(defrule
	(and
		(building-type-count-total mill >= 1)
		(and
			(dropsite-min-distance hunting <= 15)
			(dropsite-min-distance food <= 15)
		)
	)
=>
	(set-goal dependent-farms 1)
	(chat-local-to-self "Disable farms, food is near")
	(disable-self)
)

(defrule
	(and
		(goal dependent-farms 0)
		(building-type-count-total mill == 0)
	)
=>
	(set-goal dependent-farms 1)
	(chat-local-to-self "Disable farms, no mill")
)

(defrule
	(current-age == feudal-age)
	(and
		(goal dependent-farms 0)
		(and
			(building-type-count-total market == 0)
			(building-type-count-total blacksmith == 0)
		)
	)
=>
	(set-goal dependent-farms 1)
	(chat-local-to-self "Disable farms, middle antiquity age up structures missing")
)

(defrule
	(timer-triggered farm-check-timer)
	(and
		(building-type-count-total mill >= 1)
		(and
			(dropsite-min-distance hunting <= 15)
			(dropsite-min-distance food <= 15)
		)
	)
=>
	(set-goal dependent-farms 1)
	(chat-local-to-self "Disable farms, food is near")
)

(defrule
	(timer-triggered farm-check-timer)
	(and
		(building-type-count-total mill >= 1)
		(or
			(dropsite-min-distance food > 15)
			(dropsite-min-distance hunting > 15)
		)
	)
=>
	(set-goal dependent-farms 0)
	(chat-local-to-self "Enable farms, food is not near")
)

(defrule
	(unit-type-count-total villager g:< desired-number-villagers)
	(can-train villager)
=>
	(train villager)
)

(defrule
	(unit-type-count-total fishing-ship g:< desired-number-fishing)
	(cc-players-unit-type-count 0 ocean-fish-class >= 1)
	(wood-amount > 180)
	(can-train fishing-ship)
=>
	(train fishing-ship)
)

(defrule
	(unit-type-count-total trade-cart g:< desired-number-carts)
(or	(players-building-type-count any-ally market >= 1)
	(players-building-type-count any-neutral market >= 1))
	(can-train trade-cart)
=>
	(train trade-cart)
)

(defrule
	(unit-type-count-total trade-cog g:< desired-number-cogs)
	(wood-amount > 200)
	(or	
		(players-building-type-count any-ally dock >= 1)
		(players-building-type-count any-neutral dock >= 1)
	)
	(can-train trade-cog)
=>
	(train trade-cog)
)

(defrule
	(unit-type-count-total construction-ship == 0)
	(can-train construction-ship)
=>
	(train construction-ship)
)

;resource dropoff deletions so the AI can keep up with diminishing resources
(defrule
	(dropsite-min-distance wood > 8)
=>
	(set-goal delete-wood-dropoff 1)
)

(defrule
	(goal delete-wood-dropoff 1)
	(building-type-count-total lumber-camp g:>= desired-number-lumbercamps)
=>
	(delete-building lumber-camp)
	(set-goal delete-wood-dropoff 0)
)

(defrule
	(or
		(dropsite-min-distance stone > 8)
		(dropsite-min-distance gold > 8)
	)
=>
	(set-goal delete-mining-dropoff 1)
)

(defrule
	(goal delete-mining-dropoff 1)
	(building-type-count-total mining-camp g:>= desired-number-miningcamps)
=>
	(delete-building mining-camp)
	(set-goal delete-mining-dropoff 0)
)