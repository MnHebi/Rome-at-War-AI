
;civ specific structure preferences

(defrule
	(true)
=>
	(up-modify-goal phase2-barracks-cap c:= 2)
	(up-modify-goal phase3-barracks-cap c:= 2)
	(up-modify-goal phase3-range-cap c:= 2)
	(up-modify-goal phase3-stable-cap c:= 2)
	(up-modify-goal phase4-barracks-cap c:= 3)
	(up-modify-goal phase4-range-cap c:= 3)
	(up-modify-goal phase4-stable-cap c:= 3)
	(up-modify-goal phase5-barracks-cap c:= 4)
	(up-modify-goal phase5-range-cap c:= 4)
	(up-modify-goal phase5-stable-cap c:= 4)
	(up-modify-goal phase5-workshop-cap c:= 4)
	
	(disable-self)
)

;Always Train Militia/Swordsmen
(defrule
	(goal current-phase 5)
	(unit-type-count-total militiaman-line g:< gl-one-percent)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)

;Always have some Spearmen
(defrule
	(goal current-phase 5)
	(unit-type-count-total spearman-line g:< gl-one-percent)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Otherwise, train them to counter Cavalry and Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-cavalry 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total spearman-line g:< desired-number-anticavalrytrash)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Use Cavalry Archers against Cavalry
(defrule
	(goal current-phase 5)
	(goal train-anti-cavalry 1)
	(unit-type-count-total cavalry-archer-line g:< desired-number-anticavalry)
	(can-train cavalry-archer-line)
=>
	(train cavalry-archer-line)
)

;Use Crossbowmen against Infantry
(defrule
	(goal current-phase 5)
	(goal train-anti-infantry 1)
	(unit-type-count-total crossbowman g:< desired-number-antiinfantry)
	(can-train crossbowman)
=>
	(train crossbowman)
)

;Use Skirmishers against Archers & Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-archer 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total skirmisher-line g:< desired-number-antiarchertrash)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)

;Use Mangonels against Archers(but not Cavalry Archers) in addition to Skirmishers
(defrule
	(goal current-phase 5)
	(goal train-anti-archer 1)
	(unit-type-count-total mangonel-line g:< desired-number-antiarcher)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)

;Scorpions are used to counter infantry, archers, cav archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-infantry 1)
		(or
			(goal train-anti-archer 1)
			(goal train-anti-cavalryarcher 1)
		)
	)
	(unit-type-count-total scorpion-line g:< desired-number-antiarcher)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)

;Scout/Light Cavalry/Auxiliary Cavalry are always trained
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(unit-type-count-total scout-cavalry-line g:< gl-one-percent)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)

;Cavalry are used as anti-siege units
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(unit-type-count-total cavalry-line g:< desired-number-antisiege)
	(can-train cavalry-line)
=>
	(train cavalry-line)
)

;Navy, generic train rules that affect <x>reme etc training are in military-units-common.per

;Train Juggernauts
(defrule
	(goal current-phase 5)
	(wood-amount > 250)
	(unit-type-count-total juggernaut-line g:< desired-number-juggernauts)
	(can-train juggernaut-line)
=>
	(train juggernaut-line)
)

(defrule
	(goal current-phase 5)
	(unit-type-count-total peltast-line g:< desired-number-uniqueunitone)
	(can-train peltast-line)
=>
	(train peltast-line)
)

;unique technologies

;Embolon for Team Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 1)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-embolon)
=>
	(release-escrow food)
	(release-escrow gold)
	(train ut-embolon)
	(chat-local-to-self "research embolon(UT)")
)

;Embolon for Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal team-game 0)
	(can-train-with-escrow ut-embolon)
=>
	(release-escrow food)
	(release-escrow gold)
	(train ut-embolon)
	(chat-local-to-self "research embolon(UT)")
)

;Rhomphaia for Team Games, will not be done in Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 0)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-rhomphaia)
=>
	(release-escrow food)
	(release-escrow gold)
	(train ut-rhomphaia)
	(chat-local-to-self "research rhomphaia(UT)")
)

;Wicker Peltes for Team Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 0)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-wicker-peltes)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-wicker-peltes)
	(chat-local-to-self "research wicker peltes(UT)")
)

;Wicker Peltes for Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal team-game 0)
	(can-train-with-escrow ut-wicker-peltes)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-wicker-peltes)
	(chat-local-to-self "research wicker peltes(UT)")
)

;Thureophoroi for Team Games, will not be done in Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 1)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-thureophoroi)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-thureophoroi)
	(chat-local-to-self "research thureophoroi(UT)")
)