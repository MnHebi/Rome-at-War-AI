
;civ specific structure preferences

(defrule
	(true)
=>
	(up-modify-goal phase2-farm-cap c:= 10)
	(up-modify-goal phase3-farm-cap c:= 16)
	(up-modify-goal phase4-farm-cap c:= 29)
	(up-modify-goal phase5-farm-cap c:= 40)
	
	(disable-self)
)

(defrule
	(true)
=>
	(up-modify-goal phase2-barracks-cap c:= 2)
	(up-modify-goal phase3-barracks-cap c:= 2)
	(up-modify-goal phase3-range-cap c:= 2)
	(up-modify-goal phase3-stable-cap c:= 2)
	(up-modify-goal phase4-barracks-cap c:= 2)
	(up-modify-goal phase4-range-cap c:= 4)
	(up-modify-goal phase4-stable-cap c:= 4)
	(up-modify-goal phase5-barracks-cap c:= 2)
	(up-modify-goal phase5-range-cap c:= 4)
	(up-modify-goal phase5-stable-cap c:= 5)
	(up-modify-goal phase5-workshop-cap c:= 2)
	
	(disable-self)
)

;copy of the hunting rules for Scythians so they hunt deer from the start of the game
(defrule
    (dropsite-min-distance live-boar s:> sn-maximum-hunt-drop-distance)
	(unit-type-count villager >= 8)
(or	(research-completed ri-loom)
    (dropsite-min-distance live-boar < 15))
=>
   (set-strategic-number sn-enable-boar-hunting 1)
   (set-strategic-number sn-minimum-boar-lure-group-size 1)
   (set-strategic-number sn-minimum-boar-hunt-group-size 1)
   (set-strategic-number sn-minimum-number-hunters 1)
)

(defrule
	(food-amount > 200)
	(wood-amount > 200)
	(dropsite-min-distance hunting s:< sn-maximum-hunt-drop-distance)
	(unit-type-count-total hc-deer g:< gl-five-percent)
	(can-train hc-deer)
=>
	(train hc-deer)
)

;Always have some Spearmen
(defrule
	(goal current-phase 5)
	(unit-type-count-total spearman-line g:< gl-one-percent)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Otherwise, train them to counter Cavalry and Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-cavalry 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total spearman-line g:< desired-number-anticavalrytrash)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Use Archers against Infantry & Cavalry
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-infantry 1)
		(goal train-anti-cavalry 1)
	)
	(unit-type-count-total bowman-line g:< desired-number-antiinfantry)
	(can-train bowman-line)
=>
	(train bowman-line)
)

;Use Skirmishers against Archers & Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-archer 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total skirmisher-line g:< desired-number-antiarchertrash)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)

;Use Mangonels against Archers(but not Cavalry Archers) in addition to Skirmishers
(defrule
	(goal current-phase 5)
	(goal train-anti-archer 1)
	(unit-type-count-total mangonel-line g:< desired-number-antiarcher)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)

;Scorpions are used to counter infantry, archers, cav archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-infantry 1)
		(or
			(goal train-anti-archer 1)
			(goal train-anti-cavalryarcher 1)
		)
	)
	(unit-type-count-total scorpion-line g:< desired-number-antielephant)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)

;Scout/Light Cavalry are used as anti-siege trash if anti-siege units cannot be afforded
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(and
		(not
			(can-afford-unit cavalry-line)
		)
		(and
			(not
				(can-afford-unit scorpion-line)
			)
			(not
				(can-afford-unit catapult)
			)
		)
	)
	(unit-type-count-total scout-cavalry-line g:< desired-number-antisiegetrash)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)

;Cavalry are used in addition to other anti-siege measures as anti-siege units
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(unit-type-count-total cavalry-line g:< desired-number-antisiege)
	(can-train cavalry-line)
=>
	(train cavalry-line)
)

;Scout/Light Cavalry are also as anti-priest trash
(defrule
	(goal current-phase 5)
	(goal train-anti-priest 1)
	(unit-type-count-total scout-cavalry-line g:< gl-one-percent)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)

;Navy, generic train rules that affect <x>reme etc training are in military-units-common.per

;Train Juggernauts if you cannot afford Octeres
(defrule
	(goal current-phase 5)
	(not
		(can-afford-unit octeres)
	)
	(wood-amount > 250)
	(unit-type-count-total juggernaut-line g:< desired-number-juggernauts)
	(can-train juggernaut-line)
=>
	(train juggernaut-line)
)

(defrule
	(goal current-phase 5)
	(unit-type-count-total kurgan-bound-chief-line g:< desired-number-uniqueunitone)
	(can-train kurgan-bound-chief-line)
=>
	(train kurgan-bound-chief-line)
)

; Scythians use Amazons for anti-siege
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(unit-type-count-total amazon-line g:< desired-number-antisiege)
	(can-train amazon-line)
=>
	(train amazon-line)
)

;unique technologies

;Recurve Bow for Team Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 1)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-recurve-bow)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-recurve-bow)
	(chat-local-to-self "research recurve bow(UT)")
)

;Recurve Bow for Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal team-game 0)
	(can-train-with-escrow ut-recurve-bow)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-recurve-bow)
	(chat-local-to-self "research recurve bow(UT)")
)

;Bog Iron Weapons for Team Games, will not be done in Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 0)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-bog-iron-weapons)
=>
	(release-escrow food)
	(release-escrow gold)
	(train ut-bog-iron-weapons)
	(chat-local-to-self "research bog iron weapons(UT)")
)

;Akinakes for Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal team-game 0)
	(can-train-with-escrow ut-akinakes)
=>
	(release-escrow food)
	(release-escrow gold)
	(train ut-akinakes)
	(chat-local-to-self "research akinakes(UT)")
)

;Akinakes for Team Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 1)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-akinakes)
=>
	(release-escrow food)
	(release-escrow gold)
	(train ut-akinakes)
	(chat-local-to-self "research akinakes(UT)")
)

;Gorytos for Team Games, will not be done in Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 01)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-gorytos)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-gorytos)
	(chat-local-to-self "research gorytos(UT)")
)

