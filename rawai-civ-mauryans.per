
;civ specific structure preferences

(defrule
	(goal current-phase 1)
=>
	(up-modify-goal desired-number-towncenters c:= 1)
	(up-modify-goal desired-number-barracks c:= 1)
	(up-modify-goal desired-number-ranges c:= 0)
	(up-modify-goal desired-number-stables c:= 0)
	(up-modify-goal desired-number-workshops c:= 0)
	(up-modify-goal desired-number-blacksmiths c:= 0)
	(up-modify-goal desired-number-markets c:= 0)
	(up-modify-goal desired-number-docks c:= 1)
	(up-modify-goal desired-number-universities c:= 0)
	(up-modify-goal desired-number-monasteries c:= 0)
	
	(disable-self)
)

(defrule
	(goal current-phase 1)
=>
	(up-modify-goal desired-number-castles c:= 0)
	(up-modify-goal desired-number-outposts c:= 0)
	(up-modify-goal desired-number-watchtowers c:= 0)
	(up-modify-goal desired-number-seatowers c:= 0)
	(up-modify-goal desired-number-shipyards c:= 0)
	
	(disable-self)
)

(defrule
	(goal current-phase 2)
=>
	(up-modify-goal desired-number-towncenters c:= 1)
	(up-modify-goal desired-number-barracks c:= 1)
	(up-modify-goal desired-number-ranges c:= 1)
	(up-modify-goal desired-number-stables c:= 1)
	(up-modify-goal desired-number-workshops c:= 0)
	(up-modify-goal desired-number-blacksmiths c:= 1)
	(up-modify-goal desired-number-markets c:= 1)
	(up-modify-goal desired-number-docks c:= 1)
	(up-modify-goal desired-number-universities c:= 1)
	(up-modify-goal desired-number-monasteries c:= 0)
	
	(disable-self)
)

(defrule
	(goal current-phase 2)
=>
	(up-modify-goal desired-number-castles c:= 0)
	(up-modify-goal desired-number-outposts c:= 0)
	(up-modify-goal desired-number-watchtowers c:= 0)
	(up-modify-goal desired-number-seatowers c:= 0)
	(up-modify-goal desired-number-shipyards c:= 0)
	
	(disable-self)
)

(defrule
	(goal current-phase 3)
=>
	(up-modify-goal desired-number-towncenters c:= 1)
	(up-modify-goal desired-number-barracks c:= 1)
	(up-modify-goal desired-number-ranges c:= 1)
	(up-modify-goal desired-number-stables c:= 1)
	(up-modify-goal desired-number-workshops c:= 0)
	(up-modify-goal desired-number-blacksmiths c:= 1)
	(up-modify-goal desired-number-markets c:= 1)
	(up-modify-goal desired-number-docks c:= 2)
	(up-modify-goal desired-number-universities c:= 1)
	(up-modify-goal desired-number-monasteries c:= 0)
	
	(disable-self)
)

(defrule
	(goal current-phase 3)
=>
	(up-modify-goal desired-number-castles c:= 0)
	(up-modify-goal desired-number-outposts c:= 2)
	(up-modify-goal desired-number-watchtowers c:= 4)
	(up-modify-goal desired-number-seatowers c:= 4)
	(up-modify-goal desired-number-shipyards c:= 2)
	
	(disable-self)
)

(defrule
	(goal current-phase 4)
=>
	(up-modify-goal desired-number-towncenters c:= 4)
	(up-modify-goal desired-number-barracks c:= 2)
	(up-modify-goal desired-number-ranges c:= 2)
	(up-modify-goal desired-number-stables c:= 3)
	(up-modify-goal desired-number-workshops c:= 1)
	(up-modify-goal desired-number-blacksmiths c:= 1)
	(up-modify-goal desired-number-markets c:= 1)
	(up-modify-goal desired-number-docks c:= 2)
	(up-modify-goal desired-number-universities c:= 1)
	(up-modify-goal desired-number-monasteries c:= 1)
	
	(disable-self)
)

(defrule
	(goal current-phase 4)
=>
	(up-modify-goal desired-number-castles c:= 1)
	(up-modify-goal desired-number-outposts c:= 4)
	(up-modify-goal desired-number-watchtowers c:= 10)
	(up-modify-goal desired-number-seatowers c:= 5)
	(up-modify-goal desired-number-shipyards c:= 2)
	
	(disable-self)
)

(defrule
	(goal current-phase 5)
=>
	(up-modify-goal desired-number-towncenters c:= 8)
	(up-modify-goal desired-number-barracks c:= 3)
	(up-modify-goal desired-number-ranges c:= 4)
	(up-modify-goal desired-number-stables c:= 5)
	(up-modify-goal desired-number-workshops c:= 2)
	(up-modify-goal desired-number-blacksmiths c:= 1)
	(up-modify-goal desired-number-markets c:= 2)
	(up-modify-goal desired-number-docks c:= 2)
	(up-modify-goal desired-number-universities c:= 1)
	(up-modify-goal desired-number-monasteries c:= 4)
	
	(disable-self)
)

(defrule
	(goal current-phase 5)
=>
	(up-modify-goal desired-number-castles c:= 4)
	(up-modify-goal desired-number-outposts c:= 6)
	(up-modify-goal desired-number-watchtowers c:= 10)
	(up-modify-goal desired-number-seatowers c:= 5)
	(up-modify-goal desired-number-shipyards c:= 8)
	
	(disable-self)
)

;Always Train Militia/Swordsmen
(defrule
	(goal current-phase 5)
	(unit-type-count-total militiaman-line g:< desired-number-antiinfantry)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)

;Always have some Spearmen
(defrule
	(goal current-phase 5)
	(unit-type-count-total spearman-line g:< gl-one-percent)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Otherwise, train them to counter Cavalry and Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-cavalry 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total spearman-line g:< desired-number-anticavalrytrash)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Use Archers against Cavalry
(defrule
	(goal current-phase 5)
	(goal train-anti-cavalry 1)
	(unit-type-count-total bowman-line g:< desired-number-anticavalry)
	(can-train bowman-line)
=>
	(train bowman-line)
)

;Use Elephant Archers against Infantry & Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-infantry 1)
		(goal train-anti-archer 1)
	)
	(unit-type-count-total elephant-archer-line g:< desired-number-antiinfantry)
	(can-train elephant-archer-line)
=>
	(train elephant-archer-line)
)

;Use Skirmishers against Archers & Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-archer 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total skirmisher-line g:< desired-number-antiarchertrash)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)

;Use Mangonels against Archers(but not Cavalry Archers) in addition to Skirmishers
(defrule
	(goal current-phase 5)
	(goal train-anti-archer 1)
	(unit-type-count-total mangonel-line g:< desired-number-antiarcher)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)

;Scorpions are used to counter infantry, archers, cav archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-infantry 1)
		(or
			(goal train-anti-archer 1)
			(goal train-anti-cavalryarcher 1)
		)
	)
	(unit-type-count-total scorpion-line g:< desired-number-antiarcher)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)

;Mauryans wield Battle Elephants against Infantry, Archers, and Siege
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-infantry 1)
		(or
			(goal train-anti-archer 1)
			(goal train-anti-cavalry 1)
		)
	)
	(unit-type-count-total battle-elephant-line g:< desired-number-antisiege)
	(can-train battle-elephant-line)
=>
	(train battle-elephant-line)
)

;Mauryans wield Ratha against Infantry
(defrule
	(goal current-phase 5)
	(goal train-anti-infantry 1)
	(unit-type-count-total ratha-melee-line g:< desired-number-antiinfantry)
	(can-train ratha-melee-line)
=>
	(train ratha-melee-line)
)

;Scout/Light Cavalry are also used as anti-priest & anti-siege trash
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-priest 1)
		(goal train-anti-siege 1)
	)
	(unit-type-count-total scout-cavalry-line g:< gl-one-percent)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)

;Navy, generic train rules that affect <x>reme etc training are in military-units-common.per
;Train Quadriremes/Quinqueremes to complement our <x>remes and Hemiolia
(defrule
	(goal current-phase 5)
	(or
		(goal train-remes 1)
		(goal train-hemiolia 1)
	)
	(wood-amount > 300)
	(unit-type-count-total quadrireme-line g:< desired-number-qremes)
	(can-train quadrireme-line)
=>
	(train quadrireme-line)
)

;Train Juggernauts if you cannot afford Octeres
(defrule
	(goal current-phase 5)
	(not
		(can-afford-unit octeres)
	)
	(wood-amount > 250)
	(unit-type-count-total juggernaut-line g:< desired-number-juggernauts)
	(can-train juggernaut-line)
=>
	(train juggernaut-line)
)

;Else train Octeres
(defrule
	(goal current-phase 5)
	(wood-amount > 350)
	(unit-type-count-total octeres g:< desired-number-octeres)
	(can-train octeres)
=>
	(train octeres)
)

(defrule
	(goal current-phase 5)
	(goal train-anti-infantry 1)
	(unit-type-count-total chakram-thrower-line g:< desired-number-uniqueunittwo)
	(can-train chakram-thrower-line)
=>
	(train chakram-thrower-line)
)

; Upgrade Chakram Thrower
(defrule
	(up-compare-goal villager-count g:>= upgrade-military-villager-requirement)
	(goal upgrade-military-lines 1)
	(can-research-with-escrow ri-elite-chakram-thrower)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-elite-chakram-thrower)
	(chat-local-to-self "research elite chakram-thrower")
)

;make Pillars of Ashoka(not Ahsoka)
;5 in own base, 5 forward towards the enemy
(defrule
	(building-type-count-total pillar-of-ashoka < 5)
	(can-build-with-escrow pillar-of-ashoka)
=>
	(release-escrow stone)
	(build pillar-of-ashoka)
)

(defrule
	(building-type-count-total pillar-of-ashoka < 5)
	(can-build-with-escrow pillar-of-ashoka)
=>
	(release-escrow stone)
	(build-forward pillar-of-ashoka)
)

;unique technologies

;Kurpasa for Team Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 1)
		(goal team-game YES)
	)
	(can-research-with-escrow ut-kurpasa)
=>
	(release-escrow food)
	(release-escrow wood)
	(research ut-kurpasa)
	(chat-local-to-self "research kurpasa(UT)")
)

;Kurpasa for Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal team-game NO)
	(can-research-with-escrow ut-kurpasa)
=>
	(release-escrow food)
	(release-escrow wood)
	(research ut-kurpasa)
	(chat-local-to-self "research kurpasa(UT)")
)

;Mantriparishad for Team Games, will not be done in Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 0)
		(goal team-game YES)
	)
	(can-research-with-escrow ut-mantriparishad)
=>
	(release-escrow food)
	(release-escrow gold)
	(research ut-mantriparishad)
	(chat-local-to-self "research mantriparishad(UT)")
)

;Edicts of Ashoka for Team Games, will not be done in Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 1)
		(goal team-game YES)
	)
	(can-research-with-escrow ut-edicts-of-ashoka)
=>
	(release-escrow food)
	(release-escrow wood)
	(research ut-edicts-of-ashoka)
	(chat-local-to-self "research edicts of ashoka(UT)")
)

;Arthashastra for Team Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 0)
		(goal team-game YES)
	)
	(can-research-with-escrow ut-arthashastra)
=>
	(release-escrow wood)
	(release-escrow gold)
	(research ut-arthashastra)
	(chat-local-to-self "research arthashastra(UT)")
)

;Arthashastra for Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal team-game NO)
	(can-research-with-escrow ut-arthashastra)
=>
	(release-escrow wood)
	(release-escrow gold)
	(research ut-arthashastra)
	(chat-local-to-self "research arthashastra(UT)")
)