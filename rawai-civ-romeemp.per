
(defrule
	(true)
=>
	(up-modify-goal phase2-barracks-cap c:= 2)
	(up-modify-goal phase3-barracks-cap c:= 2)
	(up-modify-goal phase3-range-cap c:= 2)
	(up-modify-goal phase3-stable-cap c:= 2)
	(up-modify-goal phase4-barracks-cap c:= 3)
	(up-modify-goal phase4-range-cap c:= 2)
	(up-modify-goal phase4-stable-cap c:= 2)
	(up-modify-goal phase5-barracks-cap c:= 5)
	(up-modify-goal phase5-range-cap c:= 2)
	(up-modify-goal phase5-stable-cap c:= 2)
	(up-modify-goal phase5-workshop-cap c:= 2)
	
	(disable-self)
)

;Always Train Militia/Swordsmen
(defrule
	(goal current-phase 5)
	(unit-type-count-total militiaman-line g:< desired-number-antiinfantry)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)

;Always have some Spearmen
(defrule
	(goal current-phase 5)
	(unit-type-count-total spearman-line g:< gl-one-percent)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Otherwise, train them to counter Cavalry and Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-cavalry 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total spearman-line g:< desired-number-anticavalrytrash)
	(can-train spearman-line)
=>
	(train spearman-line)
)

;Use Archers against Cavalry
(defrule
	(goal current-phase 5)
	(goal train-anti-cavalry 1)
	(unit-type-count-total bowman-line g:< desired-number-anticavalry)
	(can-train bowman-line)
=>
	(train bowman-line)
)

;Use Crossbowmen against Infantry
(defrule
	(goal current-phase 5)
	(goal train-anti-infantry 1)
	(unit-type-count-total crossbowman g:< desired-number-antiinfantry)
	(can-train crossbowman)
=>
	(train crossbowman)
)

;Use Skirmishers against Archers & Cavalry Archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-archer 1)
		(goal train-anti-cavalryarcher 1)
	)
	(unit-type-count-total skirmisher-line g:< desired-number-antiarchertrash)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)

;Use Mangonels against Archers(but not Cavalry Archers) in addition to Skirmishers
(defrule
	(goal current-phase 5)
	(goal train-anti-archer 1)
	(unit-type-count-total mangonel-line g:< desired-number-antiarcher)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)

;Scorpions are used to counter infantry, archers, cav archers
(defrule
	(goal current-phase 5)
	(or
		(goal train-anti-infantry 1)
		(or
			(goal train-anti-archer 1)
			(goal train-anti-cavalryarcher 1)
		)
	)
	(unit-type-count-total scorpion-line g:< desired-number-antielephant)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)

;Scout/Light Cavalry are used as anti-siege trash if anti-siege units cannot be afforded
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(and
		(not
			(can-afford-unit cavalry-line)
		)
		(and
			(not
				(can-afford-unit scorpion-line)
			)
			(not
				(can-afford-unit catapult)
			)
		)
	)
	(unit-type-count-total scout-cavalry-line g:< desired-number-antisiegetrash)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)

;Catapults are used when there are enemy siege weapons present
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(unit-type-count-total catapult g:< desired-number-antisiege)
	(can-train catapult)
=>
	(train catapult)
)

;Cavalry are used in addition to other anti-siege measures as anti-siege units
(defrule
	(goal current-phase 5)
	(goal train-anti-siege 1)
	(unit-type-count-total cavalry-line g:< desired-number-antisiege)
	(can-train cavalry-line)
=>
	(train cavalry-line)
)

;Scout/Light Cavalry are also as anti-priest trash
(defrule
	(goal current-phase 5)
	(goal train-anti-priest 1)
	(unit-type-count-total scout-cavalry-line g:< gl-one-percent)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)

;Navy, generic train rules that affect <x>reme etc training are in military-units-common.per
;Train Quadriremes/Quinqueremes to complement our <x>remes and Hemiolia
(defrule
	(goal current-phase 5)
	(or
		(goal train-remes 1)
		(goal train-hemiolia 1)
	)
	(wood-amount > 300)
	(unit-type-count-total quadrireme-line g:< desired-number-qremes)
	(can-train quadrireme-line)
=>
	(train quadrireme-line)
)

;Train Juggernauts if you cannot afford Octeres
(defrule
	(goal current-phase 5)
	(not
		(can-afford-unit octeres)
	)
	(wood-amount > 250)
	(unit-type-count-total juggernaut-line g:< desired-number-juggernauts)
	(can-train juggernaut-line)
=>
	(train juggernaut-line)
)

;Else train Octeres
(defrule
	(goal current-phase 5)
	(wood-amount > 350)
	(unit-type-count-total octeres g:< desired-number-octeres)
	(can-train octeres)
=>
	(train octeres)
)

(defrule
	(goal current-phase 5)
	(unit-type-count-total legionary-ranged-line g:< desired-number-uniqueunitone)
	(can-train legionary-ranged-line)
=>
	(train legionary-ranged-line)
)

(defrule
	(goal current-phase 5)
	(goal train-anti-cavalry 1)
	(unit-type-count-total praetorian-prefect-line g:< desired-number-anticavalry)
	(can-train praetorian-prefect-line)
=>
	(train praetorian-prefect-line)
)

;unique technologies

;Siege Ballistae for Team Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 1)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-siege-ballistae)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-siege-ballistae)
	(chat-local-to-self "research siege ballistae(UT)")
)

;Siege Ballistae for Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal team-game 0)
	(can-train-with-escrow ut-siege-ballistae)
=>
	(release-escrow wood)
	(release-escrow gold)
	(train ut-siege-ballistae)
	(chat-local-to-self "research siege ballistae(UT)")
)

;Castra Network for Team Games, will not be done in Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 0)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-castra-network)
=>
	(release-escrow wood)
	(release-escrow stone)
	(train ut-castra-network)
	(chat-local-to-self "research castra network(UT)")
)

;Pax Romana for Team Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 0)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-pax-romana)
=>
	(release-escrow food)
	(release-escrow gold)
	(train ut-siege-ballistae)
	(chat-local-to-self "research pax romana(UT)")
)

;Pax Romana for Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(goal team-game 0)
	(can-train-with-escrow ut-siege-ballistae)
=>
	(release-escrow food)
	(release-escrow gold)
	(train ut-pax-romana)
	(chat-local-to-self "research pax romana(UT)")
)

;Diocletian Reforms for Team Games, will not be done in Solo Games
(defrule
	(up-compare-goal villager-count g:>= upgrade-other-villager-requirement)
	(goal upgrade-other 1)
	(and
		(goal flank-position 1)
		(goal team-game 1)
	)
	(can-train-with-escrow ut-diocletian-reforms)
=>
	(release-escrow wood)
	(release-escrow stone)
	(train ut-diocletian-reforms)
	(chat-local-to-self "research diocletian reforms(UT)")
)

